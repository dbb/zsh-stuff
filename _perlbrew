#compdef perlbrew

_perlbrew () {
    local curcontext="$curcontext" state line
    typeset -A opt_args
    
    _arguments \
        '1: :->command'\
        '*: :->perl'

    case $state in
    command)
        _arguments "1:commands:((init\:'Initialize perlbrew environment.' \
            install\:'Install perl' \
            uninstall\:'Uninstall the given installation' \
            list\:'List installed perls' \
            use\:'Use the specified perl in current shell' \
            available\:'List perls available to install' \
            switch\:'Permanently use the specified perl as default' \
            mirror\:'Pick a preferred mirror site' \
            off\:'Permanently turn off perlbrew, go back to system Perl' \
            version\:'Display version' \
            help\:'Read more detailed instructions'))"
    ;;
    *)
        case $words[2] in
        use)
            compadd "$@" ${(f)"$( ls $PERLBREW_ROOT/perls )"}
        ;;
        uninstall)
            compadd "$@" ${(f)"$( ls $PERLBREW_ROOT/perls )"}
        ;;
        *)
            _files
        esac
    esac

} ## end _perlbrew

_perlbrew "$@"

